iscr
inst
reps 6
	new: simp 2 13 6 "grasshopper" 48 0 2500
	accg 2
	perm 99
	tick 8
	attr 195
	aero 5
	elas 1
	bhvr 16
	setv ov30 0
	setv ov31 11
	setv ov32 22
	setv ov33 26
	setv ov34 30
	setv ov35 39
	setv ov36 0

	setv ov00 0
	setv ov01 0
	setv ov02 200
	setv ov05 2
	setv ov06 rand 0 1
	setv ov10 -1
	setv ov11 -1
	seta ov16 null
	setv ov20 0
	setv ov61 42
	setv ov72 200
	setv ov73 400
	setv ov74 800
	setv ov75 0

	setv va50 rand 1700 2300
	setv va51 rand 700 700
	mvto va50 va51
	setv vely 0
repe
targ ownr

endm

scrp 2 13 6 9
	addv ov01 1
	subv ov02 1
	addv ov20 1

	doif ov20 > 200 and ov00 = 0
		setv ov00 6
	endi

	doif ov02 < ov73
		setv ov00 1
	endi


	doif ov02 <= 0 or ov01 > 2000
		setv ov00 99
	endi

	setv va60 prop grid ownr left 5
	setv va61 prop grid ownr rght 5
	doif va60 > va61
		setv ov10 1
	elif va60 < va61
		setv ov10 -1
	endi

	doif rtyp room ownr = 8
		subv ov02 50
	endi

	doif obst 0 lt 30
		setv ov10 1
		setv ov11 -1
	endi
	doif obst 1 lt 30
		setv ov10 -1
		setv ov11 -1
	endi

	doif obst 2 lt 30
		setv ov11 1
	endi
	doif obst 3 lt 30
		setv ov11 -1
	endi

	doif ov02 <= 0 or ov01 > 2500
		setv ov00 99
	endi

	doif ov00 eq 99
		gsub die_
	endi

	doif ov00 = 1
		gsub gfod
	endi

	doif ov00 = 6
		esee 2 13 6
			addv va04 1
		next
		doif va04 < 2
			gsub layg
		else
			setv ov00 0
			setv ov20 0
		endi
	endi

	doif ov00 eq 0
		gsub roam
	endi


	subr die_
		setv va50 posl
		setv va51 post
		setv va52 ov10

		inst

		new: simp 2 10 19 "grasshopper" 18 30 2000
		accg 2
		elas 5
		fric 80
		tick 4
		attr 195
		aero 0
		bhvr 16
		setv ov10 va52
		setv ov61 12
		setv ov01 0
		doif tmvt va50 va51 <> 1
			mvsf va50 va51
		else
			mvto va50 va51
		endi
		setv vely 0
		anim []
		doif ov10 <= 0
			anim []
			base 0
			anim [0 1 2 3 4 5]
		else
			anim []
			base 6
			anim [0 1 2 3 4 5]
		endi
		kill ownr
	retn

	subr gfod
		setv va47 2
		setv va48 6
		setv va49 0
		inst
		doif ov16 = null
			gsub find
		endi
		targ ov16
		doif targ <> null
			targ ownr
			gsub hunt
			doif touc ov16 ownr <> 0
				targ ownr
				addv ov02 ov72
				mesg writ ov16 12
				doif ov02 gt ov74
					setv ov00 0
				endi
			endi
		else
			targ ownr
			setv ov00 0
			seta ov16 null
		endi
		targ ownr
		gsub jump
		stop
	retn



	subr layg
		setv va50 posl
		setv va51 post
		setv va52 ov10
		inst
		new: simp 2 18 10 "grasshopper" 12 48 2000
		accg 2
		elas 5
		fric 80
		bhvr 16
		tick 80
		attr 195

		setv ov00 0
		setv ov01 0
		setv ov70 0
		setv ov61 20
		setv ov02 255
		doif tmvt va50 va51 <> 1
			kill targ
			targ ownr
			gsub jump
			setv ov70 0
			setv ov00 0
			stop

		else
			mvto va50 va51
			setv vely 0
			anim []
			doif ov10 <= 0
				base 0
			else
				base 6
			endi
			anim [0]
			targ ownr
			gsub vect
			gsub anim
			gsub move
			setv ov70 0
			setv ov00 0
			setv ov20 0
			stop
		endi
	retn


	subr chrp
		setv ov75 0
		snde "ghop"
		doif ov10 <= 0
			base ov32
			anim [0 1 2 3]
		endi

		doif ov10 > 0
			base ov33
			anim [0 1 2 3]
		endi
		stop
	retn


	subr roam
		setv va66 rand 0 10
		doif va66 lt 5
			negv ov10
		endi
		doif va66 gt 5
			setv ov11 rand -1 1
			setv ov11 -1
		endi
		doif obst down = 0
			doif rand 0 20 = 0
				gsub chrp
			endi
		endi
		doif room targ ne -1
			doif prop room targ 5 ge 0.00001
				setv va00 lorp room targ 5 0
				setv va01 torx va00
				doif va01 lt 0 and ov10 gt 0
					negv ov10
				elif va01 gt 0 and ov10 lt 0
					negv ov10
				endi
			endi
		endi
		gsub jump
		stop
	retn

	subr jump
		doif ov75 = 0
			setv ov75 1

			doif ov10 <= 0
				base ov30
				anim [0 1 2 3]
			endi

			doif ov10 > 0
				base ov31
				anim [0 1 2 3]
			endi

			gsub vect
			gsub anim
			gsub move
		endi
	retn


	subr vect
		setv va10 rand 8 8
		setv va11 rand 20 20
	retn

	subr anim
		doif vely <= 0
			doif ov10 <= 0
				base ov30
				anim [0 1 2 3 4 5]
			endi

			doif ov10 > 0
				base ov31
				anim [0 1 2 3 4 5]
			endi

		else
			doif ov10 <= 0
				base ov30
				anim [6 7 8 9 10]
			endi

			doif ov10 > 0
				base ov31
				anim [6 7 8 9 10]
			endi
		endi

	retn

	subr move
		mulv va10 ov10
		mulv va11 ov11
		velo va10 va11
	retn

	subr find
		setv va99 99999999
		seta va58 null
		inst
		esee va47 va48 va49
			doif targ <> null and prop room targ 5 <= 0.00001
				setv va88 0
				setv va50 relx ownr targ
				setv va51 rely ownr targ
				doif va50 lt 0
					negv va50
				endi
				doif va51 lt 0
					negv va51
				endi
				setv va52 va50
				mulv va52 va52
				setv va53 va51
				mulv va53 va53
				setv va54 va52
				addv va54 va53
				doif va54 lt va99
					seta va58 targ
					setv va99 va54
				endi
			endi
		next
		targ va58
		doif targ <> null
			targ ownr
			seta ov16 va58
		else
			targ ownr
			seta ov16 null
		endi
		targ ownr
		slow
	retn

	subr hunt
		inst
		targ ov16
		doif targ <> null
			setv va50 posx
*			setv va51 posy
			targ ownr
			doif posx lt va50
				setv ov10 1
			endi
			doif posx gt va50
				setv ov10 -1
			endi
*			doif posy lt va51
*				setv ov11 1
*			endi
*			doif posy gt va51
*				setv ov11 -1
*			endi
		else
			targ ownr
			setv ov00 0
			stop
		endi
	retn

endm

scrp 2 13 6 6
	inst
	setv ov75 0
	doif ov10 <= 0
		base ov30

		anim [0]

	endi
	doif ov10 > 0
		base ov31

		anim [0]
	endi
	slow
endm

scrp 2 18 10 9
	inst
	addv ov01 1
	doif ov01 <= 5
		pose ov01
	else
		setv va50 posl
		setv va51 post
		setv va52 ov10
		inst
		new: simp 2 13 6 "grasshopper" 48 0 2500
		accg 2
		perm 99
		tick 8
		attr 195
		aero 5
		elas 1
		bhvr 16

		setv ov30 0
		setv ov31 11
		setv ov32 22
		setv ov33 26
		setv ov34 30
		setv ov35 39
		setv ov36 0



		setv ov00 0
		setv ov01 0
		setv ov02 200
		setv ov05 2
		setv ov06 rand 0 1
		setv ov10 -1
		setv ov11 -1
		seta ov16 null
		setv ov20 0
		setv ov61 42
		setv ov72 200
		setv ov73 400
		setv ov74 800
		setv ov75 0

		doif tmvt va50 va51 <> 1
			kill targ
			kill ownr
		else
			mvto va50 va51
			setv vely 0
			kill ownr
		endi
	endi
endm

scrp 2 10 19 9
	addv ov01 1
	doif ov01 > 50
		doif carr = null
			doif ov10 <= 0
				anim [6 7 8]
				doif room targ <> -1 and carr = null
					altr room targ 3 0.1
					altr room targ 4 0.1
				endi
				over
			endi
			doif ov10 > 0
				anim [6 7 8]
				doif room targ <> -1 and carr = null
					altr room targ 3 0.1
					altr room targ 4 0.1
				endi
				over
			endi
			kill ownr
		endi
	endi
endm

scrp 2 13 6 12
	stim writ from 80 1
	kill ownr
endm

scrp 2 18 10 12
	stim writ from 80 1
	kill ownr
endm
scrp 2 10 19 12
	stim writ from 81 1
	kill ownr
endm

rscr

enum 2 13 6
	kill targ
next
scrx 2 13 6 9
scrx 2 13 6 6

enum 2 18 10
	kill targ
next
scrx 2 18 10 9
scrx 2 10 19 12
enum 2 10 19
	kill targ
next
scrx 2 10 19 9

endm