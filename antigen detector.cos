








new: comp 3 8 20 "infection detector" 1 0 5001
attr 199

bhvr 41
perm 64
elas 20
accg 1.64
fric 100

pat: dull 1 "infection detector" 1 6 5 0

pat: butt 2 "infection detector" 12 2 32 22 0 [] 2000 0

pat: dull 3 "infection detector" 14 5 25 0

prt: inew 0 "input" "infection detector input" 10 41 2001
prt: onew 0 "output" "infection detector output" 44 41

mvto 1903 3877
SETV OV61 100
emit 18 .35


scrp 3 8 20 3
	sndc "hit_"
	velo 0 rand -5 -10
	prt: bang rand 60 100
	stim writ from 92 1
endm

scrp 3 8 20 1
	stim writ from 90 1
	mesg writ ownr 2000
endm

scrp 3 8 20 2000
	lock
	doif ov00 = 0
		snde "bep2"
		part 2
		pose 1
		part 1
		anim [0 1 2 3 4 5 6 7 8 9 10 255]
		setv ov00 1
		tick 10
	elif ov00 = 1
		snde "bep2"
		part 2
		pose 0
		part 1
		anim [0]
		setv ov00 0
		tick 0
		part 3
		pose 0
	endi
endm

scrp 3 8 20 9
	setv va00 0
	inst
	etch 4 0 0
		doif targ <> null
			addv va00 1
			doif va00 = 1
				setv va50 82
				setv va51 -1
				setv va52 -1
				loop
					doif chem va50 > va51
						setv va51 chem va50
						setv va52 va50
					endi
					addv va50 1
				untl va50 > 89
			endi
		endi
	next
	slow
	doif va00 = 1
		doif va51 <= .25 and va51 > 0.01
			part 3
			pose 1
			prt: send 0 85
		elif va51 <= .5 and va51 > .25
			part 3
			pose 2
			prt: send 0 170
		elif va51 <= 1 and va51 > .5
			part 3
			pose 3
			prt: send 0 255
		elif va51 = 0
			prt: send 0 0
		endi
	elif va00 = 0
		part 3
		pose 0
	endi
endm

scrp 3 8 20 2001
	lock
	setv va00 _p1_
	doif va00 > 0 and ov00 = 0
		unlk
		mesg writ ownr 2000
	endi
endm


scrp 3 8 20 4
	targ from
	doif fmly eq 4
		stim writ targ 91 1
	endi
endm



rscr
enum 3 8 20
	kill targ
next
scrx 3 8 20 1
scrx 3 8 20 2000
scrx 3 8 20 2001