inst
new: simp 3 8 2 "sludgegun" 30 0 5000
attr 199
bhvr 11
elas 0
accg 7
perm 60
clac 0
setv ov10 -1

setv ov61 100
emit 18 0.2
prt: inew 0 "Sludgegun Input" " " 64 64 1000
prt: onew 0 "Sludgegun throughport" " " 44 64


mvto 2000 448


scrp 3 8 2 1000
	lock
	clac 0
	prt: send 0 _p1_

	doif _p1_ lt 0 and ov10 gt 0
		gsub left
	elif _p1_ gt 0 and ov10 lt 0
		gsub rite
	elif _p1_ eq 0
		stop
	endi



	setv va09 _p1_
	divv va09 9

	gsub fire


	subr left
		sndl "sgtn"
		anim [10 11 12 13 14 15 16 17 18 19 0]
		setv ov10 -1
		over
		fade
	retn

	subr rite
		sndl "sgtn"
		anim [0 1 2 3 4 5 6 7 8 9 10]
		setv ov10 1
		over
		fade
	retn

	subr fire
		doif carr eq game "c3_inventory"
			stop
		endi
		snde "sgfr"
		setv va80 plne
		subv va80 1
		doif ov10 lt 0

			anim [20 21 22 23 24]
			setv va00 posl

			setv va01 posy
			subv va01 30



			inst
			new: simp 2 10 25 "sludgegun" 24 30 va80
			attr 195
			accg 1
			elas 20
			perm 60
			tick 1


			setv ov61 10

			velo va09 -5
			anim [0 1 2 3 4 5 6 7 255]
			doif tmvt va00 va01 eq 1
				mvto va00 va01
			else
				kill targ
			endi
			slow
		else

			anim [25 26 27 28 29]
			setv va00 posr
			subv va00 40
			setv va01 posy
			subv va01 30



			inst
			new: simp 2 10 25 "sludgegun" 24 30 va80
			attr 195
			accg 1
			elas 20
			perm 60
			tick 1

			setv ov61 10

			velo va09 -5
			anim [8 9 10 11 12 13 14 15 255]
			doif tmvt va00 va01 eq 1
				mvto va00 va01
			else
				kill targ
			endi
			slow
		endi
	retn



	unlk
endm




scrp 3 8 2 1
	lock
	inst
	clac -1

	doif from eq pntr

		doif _p2_ lt 40
			doif ov10 gt 0
				setv va00 200
			else
				setv va00 -200
			endi

		else
			doif ov10 gt 0
				setv va00 -200
			else
				setv va00 200
			endi
		endi
	else
		doif ov10 gt 0
			setv va00 200
		else
			setv va00 -200
		endi
		stim writ from 67 1
	endi
	mesg wrt+ ownr 1000 va00 0 0
endm


scrp 3 8 2 2
	lock
	inst
	clac -1
	doif ov10 gt 0
		setv va00 -200
	else
		setv va00 200
	endi
	driv 11 -0.1
	driv 8 0.1
	mesg wrt+ ownr 1000 va00 0 0
endm


scrp 2 10 25 6
	lock
	attr 0
	snde "splt"
	anim [16 17 18 19 20 21 22 23]
	over
	kill targ
endm

scrp 2 10 25 9
	lock
	setv va00 0
	etch 4 0 0
		stim writ targ 25 0

		aslp 0
		targ ownr
		attr 128
		velo 0 0
		anim [16 17 18 19 20 21 22 23]
		over
		kill targ
	next

endm

scrp 3 8 2 3
	sndc "hit_"
	velo 0 rand -5 -10
	prt: bang rand 60 100
	stim writ from 92 1
endm


scrp 3 8 2 4
	targ from
	doif fmly = 4
		stim writ targ 91 1
	endi
endm

rscr
enum 3 8 2
	kill targ
next
enum 2 10 25
	kill targ
next
scrx 3 8 2 1
scrx 2 10 25 6