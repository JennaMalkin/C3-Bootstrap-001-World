
new: simp 1 1 128 "blnk" 1 0 0
tick 3600
mvto 2000 1500



inst
reps 10
	new: simp 2 14 5 "gnat" 20 0 300
	attr 66

	bhvr 49
	tick 5
	anim [0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 255]
	setv ov10 rand 0 1
	doif ov10 eq 0
		setv ov10 -1
	endi
	setv ov11 rand 0 1
	doif ov11 eq 0
		setv ov11 -1
	endi
	mvto 1866 1658

	setv ov61 40
repe


new: simp 1 1 16 "blnk" 1 0 9000
attr 48
setv va00 wndw
setv va01 wndh
divv va00 2
divv va01 2
setv va10 wdth
divv va10 2
setv va11 hght
divv va11 2
subv va00 va10
subv va01 va11
flto va00 va01
tick 10





endm

scrp 1 1 16 9
	inst
	setv va00 0
	enum 2 14 5
		doif visi 0 eq 1
			setv va00 1
		endi
	next
	doif va00 eq 1 and ov00 eq 0
		setv ov00 1
		sndl "fly_"
	elif va00 eq 0 and ov00 eq 1
		fade
		setv ov00 0
	endi
endm


scrp 1 1 128 9
	doif totl 2 14 5 eq 0
		gsub gnat
		stop
	endi
	doif totl 2 14 4 eq 0
		gsub mosi
		stop
	endi

	subr gnat
		inst
		setv va00 posl
		setv va01 post
		new: simp 2 18 19 "blnk" 1 0 0
		doif tmvt va00 va01 <> 1
			mvsf va00 va01
		else
			mvto va00 va01

		endi
		attr 16
		tick 3600
	retn

	subr mosi
		inst
		setv va00 posl
		setv va01 post
		new: simp 2 18 20 "blnk" 1 0 0
		doif tmvt va00 va01 <> 1
			mvsf va00 va01
		else
			mvto va00 va01
		endi
		attr 16
		tick 3600
	retn
	
endm



scrp 2 14 5 9
















	addv ov01 1

	doif prop room targ 5 gt 0.2
		setv vely -10
		stop
	endi


	doif ov01 ge 100
		doif totl 2 14 5 lt 20
			rnge 1000
			setv va99 0
			esee 2 14 5
				addv va99 1
			next

			doif va99 le 3
				setv va98 totl 2 18 19

				doif va98 lt 3

					inst
					setv va90 posl
					setv va91 post
					new: simp 2 18 19 "blnk" 1 0 0
					doif tmvt va90 va91 <> 1
						kill targ
						stop
					endi
					attr 16
					tick 3600
					mvto va90 va91
				endi
			endi
		endi
	endi
	targ ownr



	setv va00 0
	setv va01 0

	doif ov00 eq 0

		setv ov14 5
		setv ov15 5

		subv ov90 1

		gsub wall

		doif ov90 le 0

			doif va00 ne 1
				doif rand 0 2 eq 0

					negv ov10
				endi
				addv ov90 rand 1 3
			endi

			doif va01 ne 1


				negv ov11

				addv ov90 rand 2 5
			endi
		endi

		gsub velo



		doif ov91 le 0
			gsub hunt

		else
			subv ov91 1
		endi

	endi


	doif ov00 eq 1
		gsub hunt
		doif posx lt ov70 and ov10 lt 0
			setv ov10 1
		endi
		doif posx gt ov70 and ov10 gt 0
			setv ov10 -1
		endi
		doif posy lt ov71 and ov11 lt 0
			setv ov11 1
		endi
		doif posy gt ov71 and ov11 gt 0
			setv ov11 -1
		endi
		setv ov14 4
		setv ov15 4

		gsub velo
		gsub wall

	endi







	subr hunt
		rnge 200
		setv va99 0
		inst
		esee 4 2 0
			setv va99 1
			setv va00 posx
			setv va01 posy
			targ ownr
			setv ov00 1
			setv ov70 va00
			setv ov71 va01
		next
		slow
		doif va99 eq 0
			setv ov00 0
		endi
	retn



	subr velo
		doif ov10 lt 0

			negv ov14
			doif ov12 gt ov14
				subv ov12 2
			endi
			negv ov14
		else

			doif ov12 lt ov14
				addv ov12 2
			endi
		endi

		doif ov11 lt 0

			negv ov15
			doif ov13 gt ov15
				subv ov13 2
			endi
			negv ov15
		else

			doif ov13 lt ov15
				addv ov13 2
			endi
		endi

		setv velx ov12
		setv vely ov13
	retn


	subr wall

		doif obst 0 lt 100 and ov10 lt 0
			negv ov10
			setv va00 1
		endi

		doif obst 1 lt 100 and ov10 gt 0
			negv ov10
			setv va00 1
		endi

		doif obst 2 lt 50 and ov11 lt 0
			negv ov11
			setv va01 1
		endi

		doif obst 3 lt 100 and ov11 gt 0
			negv ov11
			setv va01 1
		endi
	retn






endm

scrp 2 14 5 1
	setv vely -20
	targ from
	doif fmly eq 4
		stim writ targ 88 1
	endi
endm

scrp 2 14 5 4
	targ from
	doif fmly eq 4
		stim writ targ 88 1
	endi
	mesg writ ownr 5
endm

scrp 2 14 5 12
	targ from
	doif fmly eq 4
		stim writ targ 88 1
	endi
	kill ownr
endm



scrp 2 18 19 9
	inst
	setv va00 posl
	setv va01 post
	rnge 700
	setv va66 0
	esee 2 14 5
		addv va66 1
	next
	doif va66 < 20
		new: simp 2 14 5 "gnat" 20 0 300
		attr 66
		doif tmvt va00 va01 <> 1
			kill targ
			stop
		endi

		bhvr 49
		tick 5
		anim [0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 255]
		setv ov10 rand 0 1
		doif ov10 eq 0
			setv ov10 -1
		endi
		setv ov11 rand 0 1
		doif ov11 eq 0
			setv ov11 -1
		endi
		mvto va00 va01

		setv ov61 40
	endi
	kill ownr
endm







rscr
enum 2 14 5
	kill targ
next
enum 1 1 16
	kill targ
next
scrx 1 1 16 9
scrx 2 14 5 9
scrx 2 14 5 1
scrx 2 14 5 4
scrx 2 14 5 12